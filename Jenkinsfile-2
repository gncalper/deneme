pipeline {
    agent any

    parameters {
        string(name: 'WORKSPACE')
        string(name: 'PROJECT')
        string(name: 'CONFIG_DIR')
        choice( name: 'NAMESPACE',choices: ['uat', 'prod'], description: 'Is your project for testing or production?')
        choice( name: 'PROJECT_TYPE',choices: ['web', 'mobile','workflow'], description: 'Is your project for mobil, workflow or web?')
        booleanParam(name: 'HOST_PATH',defaultValue: false,description: 'Host path kullanılacak mı?')
        string(name: 'BACKEND_HOSTNAME', description: 'Uygulamanın backend veya workflow url ini girin')
        string(name: 'BACKEND_PATH', defaultValue: '', description: 'Path (Host path checkbox işaretli ise uygulamanın backend pathi var ise değer giriniz yoksa boş bırakınız)' )
        string(name: 'FRONTEND_HOSTNAME', description: 'Uygulamanın frontend url ini girin')
        string(name: 'FRONTEND_PATH', defaultValue: '', description: 'Path (Host path checkbox işaretli ise uygulamanın frontend pathi var ise değer giriniz yoksa boş bırakınız)' )
        }

    stages {

        stage('Validate Parameters') {
            steps {
                script {
                    if (!params.WORKSPACE?.trim()) {
                        error "WORKSPACE zorunludur ve boş olamaz"
                    }

                    if (!params.PROJECT?.trim()) {
                        error "PROJECT zorunludur ve boş olamaz"
                    }

                    if (!params.CONFIG_DIR?.trim()) {
                        error "CONFIG_DIR zorunludur ve boş olamaz"
                    }

                    if (!params.BACKEND_HOSTNAME?.trim()) {
                        error "BACKEND_HOSTNAME zorunludur ve boş olamaz"
                    }

                    if (params.PROJECT_TYPE == 'web') {
                        if (!params.FRONTEND_HOSTNAME?.trim()) {
                            error "PROJECT_TYPE=web iken FRONTEND_HOSTNAME zorunludur"
                        }
                    }

                    if (params.HOST_PATH && !params.BACKEND_PATH?.trim()) {
                        error "HOST_PATH açıkken BACKEND_PATH zorunludur"
                    }

                    if (params.PROJECT_TYPE == 'web' && params.HOST_PATH && !params.FRONTEND_PATH?.trim()) {
                        error "HOST_PATH açıkken FRONTEND_PATH zorunludur"
                    }

                }
            }
        }

        stage('Generate Helm Values') {
            steps {
                script {
                    def httpPathValue = params.HOST_PATH ? "true" : "false"
                    def workspace = params.WORKSPACE.toLowerCase()
                    def project   = params.PROJECT.toLowerCase()
                    def backendType = "backend"

                    if (params.PROJECT_TYPE == 'workflow') {
                        backendType = "workflow"
                    }

                    def backendTpl = readFile 'templates/backend.yaml.template'

                    def backendValues = backendTpl
                        .replace('@WORKSPACE@', workspace)
                        .replace('@PROJECT@', project)
                        .replace('@NAMESPACE@', params.NAMESPACE)
                        .replace('@HOST_PATH@', httpPathValue)
                        .replace('@BACKEND_TYPE@', backendType)
                        .replace('@BACKEND_HOSTNAME@', params.BACKEND_HOSTNAME)
                        .replace('@BACKEND_PATH@', params.BACKEND_PATH)

                    writeFile file: 'values-backend.yaml', text: backendValues
                    echo "Backend values.yaml generated"

                    if (params.PROJECT_TYPE == 'web') {

                        def frontendEnvFromSecret = "false"
                        def frontendExternalSecret = "false"

                        if (params.NAMESPACE == 'prod') {
                            frontendEnvFromSecret = "true"
                            frontendExternalSecret = "true"
                        }

                        def frontendTpl = readFile 'templates/frontend.yaml.template'

                        def frontendValues = frontendTpl
                            .replace('@WORKSPACE@', workspace)
                            .replace('@PROJECT@', project)
                            .replace('@NAMESPACE@', params.NAMESPACE)
                            .replace('@HOST_PATH@', httpPathValue)
                            .replace('@FRONTEND_HOSTNAME@', params.FRONTEND_HOSTNAME)
                            .replace('@FRONTEND_PATH@', params.FRONTEND_PATH)
                            .replace('@ENV_FROM_SECRET@', frontendEnvFromSecret)
                            .replace('@EXTERNAL_SECRET_ENABLED@', frontendExternalSecret)

                        writeFile file: 'values-frontend.yaml', text: frontendValues
                        echo "Frontend values.yaml generated"
                    } else {
                        echo "PROJECT_TYPE web değil → frontend values.yaml oluşturulmadı"
                    }
                }
            }
        }

    }
}
